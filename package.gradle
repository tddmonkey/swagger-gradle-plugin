apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.tddmonkey'
version = '0.1.0'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Swagger Gradle Plugin'
                packaging 'jar'
                description 'Swagger Gradle Plugin'
                url 'https://github.com/tddmonkey/swagger-gradle-plugin'

                scm {
                    connection 'scm:git:https://github.com/tddmonkey/swagger-gradle-plugin'
                    developerConnection 'scm:git:https://github.com/tddmonkey/swagger-gradle-plugin'
                    url 'https://github.com/tddmonkey/swagger-gradle-plugin'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'tddmonkey'
                        name 'Colin Vipurs'
                        email 'zodiaczx6@gmail.com'
                    }
                }
            }
        }
    }
}
